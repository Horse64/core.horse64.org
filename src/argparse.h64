# @module argparse
# Copyright (c) 2020-2022,  ellie/@ell1e & Horse64 Team (see AUTHORS.md).
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
# 1. Redistributions of source code must retain the above copyright notice, this
#   list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above copyright notice,
#    this list of conditions and the following disclaimer in the documentation
#    and/or other materials provided with the distribution.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
# LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
# INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
# CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
# ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
# POSSIBILITY OF SUCH DAMAGE.
#
# Alternatively, at your option, this file is offered under the Apache 2
# license, see accompanied LICENSE.md.

import math from core.horse64.org
import path from core.horse64.org
import process from core.horse64.org
import system from core.horse64.org
import textformat from core.horse64.org


type _ArgumentInfo {
    var name = ""
    var catchall = no
    var multiple_uses = no
    var takes_arg = no
    var aliases = []
    var optional = no
    var positional = no
    var action_func = none
    var description = none
}


func _ArgumentInfo.init(
        name, aliases=[],
        is_switch=no, takes_arg="default",
        multiple_uses=no,
        description=none, action_func=none
        ) {
    self.description = (
        if description != none (description + "") else (none)
    )
    self.name = name + ""
    for alias in aliases {
        self.aliases.add(alias + "")
    }
    self.positional = not self.name.starts("-")
    if is_switch != (not self.positional) {
        throw ValueError("is_switch must be yes if "
            "argument begins with dash, otherwise no")
    }
    if not is_switch and multiple_uses != no {
        throw ValueError("multiple uses only allowed "
            "for switches")
    }
    self.multiple_uses = (multiple_uses == yes)
    if not is_switch and action_func != none {
        throw ValueError("action_func only allowed "
            "for switches")
    }
    self.action_func = action_func
    if takes_arg == "default" {
        if self.positional {
            self.takes_arg = yes
        } else {
            self.takes_arg = no
        }
    } elseif takes_arg == "optional" or takes_arg == "catchall" {
        self.takes_arg = yes
        if not self.positional {
            throw new ValueError("only positional arguments "
                "may take an argument value optionally or "
                "as catchall")
        }
        if takes_arg == "optional" {
            self.optional = yes
        } else {
            self.catchall = yes
        }
    } else {
        if typename(takes_arg) != "bool" {
            throw new ValueError("unsupported value for "
                "takes_arg of type: " + typename(takes_arg))
        }
        if not takes_arg and self.positional {
            throw new ValueError(
                "positonal arguments must take an "
                "argument value at least optionally"
            )
        }
        self.takes_arg = takes_arg
        self.optional = no
    }
}


func _ArgumentInfo.contains_argname(names) {
    if names.contains(self.name) {
        return yes
    }
    for alias in self.aliases {
        if names.contains(alias) {
            return yes
        }
    }
    return no
}


## The command line argument definitions to use. This @{type} collects
## what sort of argument your command line tool knows via e.g.
## @{add_arg|ArgumentDefs.add_arg}, and can then be used with
## @{parse} to parse user input into a nice little @{assembled
## result|ParseResult} for your convenience.
type ArgumentDefs {
    var args protect = []
    var min_positional_args protect = 0
    var max_positional_args protect = 0
    var catchall_exists protect = no
    var program_name = "unknown"
}


## @param program_name str (Optional) The name of your command
##   line program, should match what the actual name of the binary
##   is that the user types. If unspecified, it will be auto-detected.
func ArgumentDefs.init(program_name=none) {
    # Sanitize and guess the program name for this arguments set:
    if program_name != none and typename(program_name) != "str" {
        throw TypeError("program_name must be none or string")
    }
    if program_name == none {
        program_name = path.basename(system.self_exec_path())
        if program_name == "none" {
            program_name = "unknown"
        }
    }
    self.program_name = program_name
}


func ArgumentDefs.get_catchall_arg {
    for arg in self.args {
        if arg.positional and arg.catchall {
            return arg
        }
    }
    return none
}


func ArgumentDefs.get_nth_posarg(index, total_posargs_assigned=none) {
    var non_optional_count = 0
    for arg in self.args {
        if arg.positional and not arg.optional {
            non_optional_count = 1
        }
    }
    var optional_args_filled = 0
    if total_posargs_assigned {
        optional_args_filled =
            math.max(0,
                total_posargs_assigned - non_optional_count
            )
    }
    for arg in self.args {
        if arg.positional and
                (not arg.optional or
                optional_args_filled > 0) {
            if index > 1 {
                index -= 1
            } else {
                return arg
            }
        }
    }
    return none
}


func ArgumentDefs.get_namedarg(name) {
    if not name.starts("-") {
        return none
    }
    for arg in self.args {
        if arg.name == name or arg.aliases.contains(name) {
            return arg
        }
    }
    return none
}


## @func ArgumentDefs.add_arg
##
## Add a positional argument that must be passed in by the user.
## This might e.g. be a target file, or similar.
##
## After @{parsing|ArgumentDefs.parse}, the map of all parsed values
## will contain an entry with the name set to the passed argument as
## @{str}.
##
## Example:
## ```horse64
## var defs = argparse.ArgumentDefs()
## defs.add_arg("target_file")
## var parsed_values = argparse.parse(defs, ["some_file.txt"])
## print(parsed_values["target_file"])  # will print out "some_file.txt"!
## ```
##
## @param name str the name for the positional argument
## @param is_optional bool Specifies that the positional argument can
##   be silently omitted if set to true. Then the final map of all parsed
##   values will have the corresponding entry set to @{none} if
##   it isn't specified by the user.
## @param description str (Optional) A description for the user
##   what this argument is for.
func ArgumentDefs.add_arg(name, is_optional=no, description=none) {
    if name.starts("-") {
        throw new ValueError(
            "positional arguments can't have a name "
            "starting with a dash")
    }
    for arg in self.args {
        if arg.contains_argname([name]) {
            throw new ValueError(
                "name conflict with existing options"
            )
        }
        if arg.positional and arg.catchall {
            throw new ValueError(
                "cannot add positional argument after a catchall"
            )
        }
    }
    self.args.add(new _ArgumentInfo(
        name, is_switch=no, description=description,
        takes_arg=(if is_optional ("optional") else (yes))
    ))
    if self.args[self.args.len - 1].positional {
        self.max_positional_args += 1
        if not self.args[self.args.len - 1].optional {
            self.min_positional_args += 1
        }
    }
}


## @func ArgumentDefs.add_catchall
##
## **(Advanced functionality)**
## Add a catchall argument that will consume all arguments following
## the last positional argument. This is only useful for special
## cases, like if you want to pass them on to some nested program.
## Requires that one or more regular @{positional arguments were
## previously added|ArgumentDefs.add_arg}.
##
## After @{parsing|ArgumentDefs.parse}, the map of all parsed values
## will contain an entry with the name set to the consumed arguments as
## a @{list}.
##
## @param name str The name for the catchall argument
## @param description str (Optional) A description for the user
##   what this argument is for
func ArgumentDefs.add_catchall(name, description=none) {
    if name.starts("-") {
        throw new ValueError(
            "positional arguments can't have a name "
            "starting with a dash")
    }
    var positional_count = 0
    for arg in self.args {
        if arg.contains_argname([name]) {
            throw new ValueError(
                "name conflict with existing options"
            )
        }
        if arg.positional and not arg.catchall {
            positional_count += 1
        }
        if arg.positional and arg.catchall {
            throw new ValueError(
                "cannot add multiple catchall arguments"
            )
        }
    }
    if positional_count == 0 {
        throw new ValueError(
            "cannot add catchall without any positional arguments"
        )
    }
    self.args.add(new _ArgumentInfo(
        name, is_switch=no,
        takes_arg="catchall", description=description
    ))
    self.catchall_exists = yes
}


## @func ArgumentDefs.add_switch
##
## Add an option switch that begins with a dash that the user may or
## may not use. This usually enables optional behavior of your program.
##
## After @{parsing|ArgumentDefs.parse}, the map of all parsed values
## will contain an entry with the switch name set to @{yes} if the
## switch was specified, or otherwise @{no}.
##
## Example:
## ```horse64
## var defs = argparse.ArgumentDefs()
## defs.add_switch("--switch-1")
## defs.add_switch("--switch-2")
## var parsed_values = argparse.parse(defs, ["--switch-2"])
## print(parsed_values["switch-1"])  # will print out "no"!
## print(parsed_values["switch-2"])  # will print out "yes"!
## ```
##
## @param name str the name of a switch which must start with a dash
## @param aliases list Alternate names for this switch which also
##     must all start with a dash. Must be a list of @{str}s.
## @param takes_arg bool Specify that this switch takes an argument
##   specified right after, e.g. `--target-file path-to-file`. If set
##   to yes, the map of all parsed values will instead contain the
##   switch set to either @{none} if the switch wasn't specified,
##   or the user-given @{str} of that argument.
## @param multiple_uses bool Specify if this switch can be used
##   by the user multiple times. If yes and then the map of all
##   parsed values will contain the switch with a
##   @{numeric counter|num} instead of a @{bool} that counts the
##   uses, unless takes_arg is also set to @{yes} in which case
##   the map will contain a @{list} of all args of all uses.
## @param description str (Optional) A description for the user
##   what this argument is for.
## @param action_func func (Optional) A function that should be
##   run as soon as this switch is found during a
##   @{parse|ArgumentDefs.parse} run.
func ArgumentDefs.add_switch(
        name, aliases=[], takes_arg=no, description=none,
        multiple_uses=no,
        action_func=none
        ) {
    if not name.starts("-") {
        throw new ValueError("switch names must start with dash")
    }
    for alias in aliases {
        if not alias.startswith("-") {
            throw new ValueError("switch aliases must start with dash")
        }
    }
    if takes_arg != yes and takes_arg != no {
        throw new ValueError("takes_arg must be yes or no")
    }
    for arg in self.args {
        if arg.contains_argname([name]) or
                arg.contains_argname(aliases) {
            throw new ValueError(
                "name conflict with existing options"
            )
        }
    }
    self.args.add(new _ArgumentInfo(
        name, aliases=aliases, is_switch=yes,
        multiple_uses=multiple_uses,
        takes_arg=takes_arg, description=description,
        action_func=action_func
    ))
}


## The result of a @{parse call|parse}. Contains all the values for
## all switches and arguments in the @{args map|ParseResult.args},
## with the keys being the names of each argument or switch.
##
## If @{an output string was set|ParseResult.output} with length
## greater than zero, this means the parser returned a message for
## the user (like an error) and an @{exit code|ParseResult.exit_code}
## for your program that can be used with @{system.exit}.
## The exit code will always be set if any non-empty output string
## was set.
type ParseResult {
    ## The output string, may e.g. contain errors. Empty string if
    ## everything went okay.
    var output = ""

    ## The exit code suggested to go along with the output string.
    ## A @{num} if there is an exit code, otherwise remains @{none}.
    var exit_code = none

    ## A @{map} filled with values for all switches and arguments.
    var args
}


func get_usage_text(options) {
    var output = ""

    # Output main usage line with positional args:
    output += "Usage: " +
        options.program_name + " [..options..]"
    for arg in options.args {
        if not arg.positional {
            continue
        }
        output += " " + arg.name.replace(" ", "_") + (
            if arg.optional ("[optional]") else ("")
        ) + (if arg.catchall ("..") else ("")) + ""
    }
    output += "\n\n"

    # Output info on all positional args:
    var got_positional = no
    for arg in options.args {
        if arg.positional {
            got_positional = yes
            break
        }
    }
    if got_positional {
        output += "Arguments:\n"
        var longest_arg_len = 0
        for arg in options.args {
            if arg.positional {
                longest_arg_len = math.max(
                    longest_arg_len, arg.name.len)
            }
        }
        var indent_target_len = min(
            30, longest_arg_len + 3
        )
        for arg in options.args {
            if arg.positional {
                var spaces = " ".repeat(math.max(
                    2, indent_target_len - arg.name.len
                ))
                var spaces_after_wrap = " ".repeat(indent_target_len)
                var wrapped = textformat.wrap(
                    "   " + arg.name.replace(" ", "_") +
                    (if arg.description != none (spaces + arg.description)
                    else ("")), 79,
                    later_lines_prefix="   " + spaces_after_wrap)
                output += wrapped + "\n"
            }
        }
        output += "\n"
    }

    # Output all available option switches as a nice table:
    output += "Available Options:\n"
    var switch_desc_map = {->}
    var unsorted_names = []
    for arg in options.args {
        if not arg.name.starts("-") {
            continue
        }
        unsorted_names.add(arg.name)
        if arg.description != none and
                arg.description.len > 0 {
            switch_desc_map[arg.name] = arg.description
        }
    }
    if not unsorted_names.contains("--help") {
        unsorted_names.add("--help")
        switch_desc_map["--help"] =
            "Show this help text."
    }
    var sorted_names = unsorted_names.sort()
    var longest_switch_len = 0
    for switch_name in sorted_names {
        longest_switch_len = math.max(
            longest_switch_len, switch_name.len)
    }
    var indent_target_len = min(
        30, longest_switch_len + 3
    )
    for switch_name in sorted_names {
        var spaces = " ".repeat(math.max(
            2, indent_target_len - switch_name.len
        ))
        var spaces_after_wrap = " ".repeat(indent_target_len)
        var switch_description = none
        if switch_desc_map.contains(switch_name) {
            switch_description = switch_desc_map[switch_name]
        }
        var wrapped = textformat.wrap(
            "   " + switch_name +
            (if switch_description != none
                (spaces + switch_description)
                else ("")),
            79,
            later_lines_prefix="   " + spaces_after_wrap)
        output += wrapped + "\n"
    }

    return output
}


## @func parse
## Use the given @{command line argument definitions|ArgumentDefs}
## to parse the command line arguments given. If the args parameter
## is unspecified, this will default to @{process.args}.
##
## @returns ParseResult
## @param argument_defs ArgumentDefs The argument definitions to
##   use for parsing, which specify all known arguments and switches.
## @param args list (Optional) The list of supplied command line
##   arguments to parse. If unspecified, @{process.args} is used.
func parse(argument_defs, args=none) {
    if not has_attr(argument_defs, "max_positional_args") {
        throw new TypeError(
            "argument_defs must have the "
            "argparse.ArgumentDefs attributes"
        )
    }
    if args == none {
        args = process.args
    }
    var result = new ParseResult()
    var result_args = {->}
    var posargs_seen = 0

    # Count number of positional arguments in advance:
    # (We need the total in advance to handle optional ones right)
    var double_dash_idx = none
    var positional_args_total = 0
    var i = 0
    while i < args.len {
        var argstr = args[i]
        if not args[i].starts("-") or
                (double_dash_idx != none and
                i >= double_dash_idx) {
            positional_args_total += 1
        } elseif args[i] == "--" {
            double_dash_idx = i
            args = args.sub(0, i) +
                args.sub(i + 1)
            continue
        } else {
            var switch_with_arg = no
            for arg in argument_defs.args {
                if arg.name == args[i] or
                        arg.aliases.contains(args[i]) {
                    if arg.takes_arg {
                        switch_with_arg = yes
                    }
                    break
                }
            }
            if switch_with_arg {
                i += 2
                continue
            }
        }
        i += 1
    }

    # See from where everything needs to go into the catchall:
    # (Basically, everything after the last regular positional arg,
    # when a catchall follows, everything including switches should
    # go into it)
    var catchall_start_index = none
    var _posarg_no = 0
    i = 0
    while i < args.len {
        var argstr = args[i]
        if not args[i].starts("-") or
                (double_dash_idx != none and
                i > double_dash_idx) {
            _posarg_no += 1
            var target_arg =
                argument_defs.get_nth_posarg(
                    _posarg_no, positional_args_total)
            if target_arg != none and
                    target_arg.catchall {
                # Special case: first positional arg is already a catchall.
                # In that case, everything starting from the first
                # obvious non-switch (=here) will go inside.
                catchall_start_index = i
                break
            } else {
                # Regular case: we reached the last positional arg
                # that is regular. Everything past here (=one index further)
                # should go inside.
                var followup_arg =
                    argument_defs.get_nth_posarg(
                        _posarg_no + 1, positional_args_total)
                if followup_arg != none and
                        followup_arg.catchall {
                    catchall_start_index = i + 1
                    break
                }
            }
        }
        i += 1
    }

    # Extract all arguments:
    posargs_seen = 0
    var i = 0
    while i < args.len {
        var argstr = args[i]
        if (not args[i].starts("-")) or (catchall_start_index != none and
                i >= catchall_start_index) or
                (double_dash_idx != none and
                i >= double_dash_idx) {
            # Positional argument. See if we reached maximum:
            if posargs_seen + 1 > argument_defs.max_positional_args and
                    not argument_defs.catchall_exists {
                var xth = (posargs_seen + 1).as_str()
                if xth.endswith("1") {
                    xth += "st"
                } elseif xth.endswith("2") {
                    xth += "nd"
                } elseif xth.endswith("3") {
                    xth += "rd"
                } else {
                    xth += "th"
                }
                result.output += argument_defs.program_name + ": error: "
                    "excessive " + xth + " "
                    "positional argument: " + argstr + "\n"
                result.output += (
                    "Use " + argument_defs.program_name +
                    " --help to show usage.\n"
                )
                result.exit_code = -1
                return result
            }

            # Check what exactly this maps to:
            var target_arg =
                argument_defs.get_nth_posarg(
                    posargs_seen + 1, positional_args_total)
            if target_arg == none {
                target_arg = argument_defs.get_catchall_arg()
            }
            assert(target_arg != none)

            #print("argparse.h64: debug: feeding into "
            #    "arg \"" + target_arg.name +
            #    "\" => " + argstr)

            # Add it to the result value of what it maps to:
            if target_arg.catchall {
                # Catchall arg that collects multiple values as list:
                if not result_args.contains(target_arg.name) {
                    result_args[target_arg.name] = []
                }
                result_args[target_arg.name].append(argstr)
            } else {
                # Regular positional arg that takes exactly one value:
                result_args[target_arg.name] = argstr
            }
            posargs_seen += 1
        } else {
            # Special handling for --help:
            if argstr == "--help" {
                result.output = get_usage_text(argument_defs)
                result.exit_code = 0
                return result
            }
            # See if we can find out the arg def this matches:
            var target_arg = argument_defs.get_namedarg(argstr)
            if target_arg == none {
                result.output += argument_defs.program_name +
                    ": error: "
                    "unknown option: " + argstr + "\n"
                result.output += (
                    "Use " + argument_defs.program_name +
                    " --help to show usage.\n"
                )
                result.exit_code = -1
                return result
            }
            # Block specifying multiple times if not allowed:
            if result_args.contains(target_arg.name) and
                    not target_arg.multiple_uses and
                    result_args[target_arg] == yes {
                result.output += argument_defs.program_name +
                    ": error: "
                    "option cannot be spilled multiple times: " +
                    target_arg.name
                result.output += (
                    "Use " + argument_defs.program_name +
                    " --help to show usage.\n"
                )
                result.exit_code = -1
                return result
            }
            # If this named arg takes an argument, check that it's
            # specified after:
            if target_arg.takes_arg == yes {
                if i + 1 > args.len or
                        args[i + 1].starts("-") {
                    result.output += argument_defs.program_name +
                        ": error: "
                        "option is missing argument: " +
                        target_arg.name + "\n"
                    result.output += (
                        "Use " + argument_defs.program_name +
                        " --help to show usage.\n"
                    )
                    result.exit_code = -1
                    return result
                }
                if target_arg.multiple_uses {
                    # Can be specified multiple times, use list:
                    if not result_args.contains(target_arg.name) {
                        result_args[target_arg.name] = []
                    }
                    result_args[target_arg.name].add(args[i + 1])
                } else {
                    result_args[target_arg.name] = args[i + 1]
                }
                i += 2
                continue
            } else {
                if target_arg.multiple_uses {
                    # Can be specified multiple times, use counter:
                    if not result_args.contains(target_arg.name) {
                        result_args[target_arg.name] = 0
                    }
                    result_args[target_arg.name] += 1
                } else {
                    result_args[target_arg.name] = yes
                }
            }
        }
        i += 1
    }
    # Fill in empty spots in result dictionary:
    for arg in argument_defs.args {
        if not arg.positional and
                not result_args.contains(arg.name) {
            if arg.takes_arg and not arg.catchall and
                    not arg.multiple_uses {
                result_args[arg.name] = none
            } elseif arg.catchall or (arg.takes_arg and
                    arg.multiple_uses) {
                result_args[arg.name] = []
            } elseif not arg.takes_arg and arg.multiple_uses {
                result_args[arg.name] = 0
            } else {
                result_args[arg.name] = no
            }
        }
    }
    # Collect all switches that will have an action_func run:
    var action_switch_names = []
    for arg in argument_defs.args {
        if not arg.positional and
                arg.action_func != none and
                result_args.contains(arg.name) and
                result_args[arg.name] != none and
                result_args[arg.name] != no {
            # A switch with an action. Collect it for later sorting:
            action_switch_names.add(arg.name)
        }
    }
    # Get each action_func to be run in alphabetical switch order:
    action_switch_names = action_switch_names.sort()
    var action_funcs_in_order = []
    for action_switch_name in action_switch_names {
        for arg in argument_defs.args {
            if arg.name == action_switch_name {
                action_funcs_in_order.add(arg.action_func)
                break
            }
        }
    }
    # Run each action_func in order:
    for action_func in action_funcs_in_order {
        var result_value = action_func()
        if typename(result_value) == "str" and
                result.exit_code == none {
            result.output = result_value
            result.exit_code = 0
        }
    }
    # Check if we even have the minimum number of required arguments:
    # (Needs to be after action switches, so e.g. --help still works)
    if positional_args_total < argument_defs.min_positional_args and
            result.exit_code == none {
        var target_arg = argument_defs.get_nth_posarg(
            positional_args_total
        )
        result.output += argument_defs.program_name + ": error: "
            "missing positional argument: " +
            target_arg.name + "\n"
        result.output += (
            "Use " + argument_defs.program_name +
            " --help to show usage.\n"
        )
        result.exit_code = -1
        return result
    }
    # Return result:
    result.args = result_args
    return result
}
