## @module compiler.ast.call_or_assign_stmt
# Copyright (c) 2023-2024, ellie/@ell1e & Horse64 Team (see AUTHORS.md).
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
# 1. Redistributions of source code must retain the above copyright notice,
#    this list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above copyright notice,
#    this list of conditions and the following disclaimer in the documentation
#    and/or other materials provided with the distribution.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
# LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
# INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
# CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
# ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
# POSSIBILITY OF SUCH DAMAGE.
#
# Alternatively, at your option, this file is offered under the Apache 2
# license, see accompanied LICENSE.md.

import compiler.ast as ast
import compiler.ast.expr as expr
import compiler.ast.invalid_stmt as invalid_stmt
import compiler.ast.later_parse as later_parse
import compiler.ast.block as block
import compiler.msg as msg
import compiler.token as token

type CallStmt base ast.StmtNode {
    var has_later = no
    var has_later_ignore = no
    var has_later_repeat = no
}

func CallStmt.init {
    base.init()
    self.kind = ast.N_STMT_CALL
}

func CallStmt.as_json_obj {
    var output = base.as_json_obj()
    if self.has_later {
        output["has_later"] = yes
    }
    if self.has_later_ignore {
        output["has_later_ignore"] = yes
    } elseif self.has_later {
        output["has_later_ignore"] = no
    }
    if self.has_later_repeat {
        output["has_later_repeat"] = yes
    } elseif self.has_later {
        output["has_later_repeat"] = no
    }
    return output
}

func CallStmt.as_code(indent=0) {
    if self.damaged {
        return " ".rep(indent) + "(damaged call statement)"
    }
    return " ".rep(indent) + self.subexprs[1].as_code(indent=indent)
}

type AssignStmt base ast.StmtNode {
    var has_later = no
    var has_later_ignore = no
    var has_later_repeat = no
    var assign_token
}

func AssignStmt.init(token) {
    base.init()
    self.kind = ast.N_STMT_ASSIGN
    assert(token != none)
    assert(typename(token.str) == "str")
    self.assign_token = token
}

func AssignStmt.as_json_obj {
    var output = base.as_json_obj()
    if self.has_later {
        output["has_later"] = yes
    }
    if self.has_later_ignore {
        output["has_later_ignore"] = yes
    }
    if self.has_later_repeat {
        output["has_later_repeat"] = yes
    }
    return output
}

func AssignStmt.as_code(indent=0) {
    if self.damaged {
        return " ".rep(indent) + "(damaged assign)"
    }
    var pairs_count = self.subexprs.len / 2
    var lvalue_list = ""
    var rvalue_list = ""
    var i = 1
    while i <= pairs_count {
        if i > 1 {
            lvalue_list += ", "
            rvalue_list += ", "
        }
        lvalue_list += self.subexprs[i].as_code(indent=indent)
        rvalue_list += self.subexprs[pairs_count + i].
            as_code(indent=indent)
        i += 1
    }
    var t = " ".rep(indent) +
        lvalue_list + " " + self.assign_token.str +
        " " + rvalue_list
    if self.has_later {
        if self.has_later_ignore {
            t += " later ignore"
        } elseif self.has_later_repeat {
            t += " later repeat"
        } else {
            t += " later:"
        }
    }
    return t
}

func check_call_or_assign_and_get_idx(
        tokens, pos, tokens_len, only_assigns=no, debug=no) {
    if pos > tokens_len or (
            not {token.T_NUM, token.T_STR, token.T_BYTES,
             token.T_IDENT}.has(tokens[pos].kind) and
            (tokens[pos].kind != token.T_KEYWORD or
             tokens[pos].str != "base")) {
        return -1
    }

    # To check this is a call or assign, find the '(' or '=':
    var is_call = yes
    var can_be_call = yes
    var assign_or_call_idx = -1
    var i = pos + 1
    while yes {
        if i > tokens_len {
            break
        } elseif tokens[i].kind == token.T_DOT or
                tokens[i].kind == token.T_COMMA {
            if tokens[i].kind == token.T_COMMA {
                can_be_call = no
            }
            if i + 1 > tokens_len or
                    tokens[i + 1].kind != token.T_IDENT {
                break
            }
            i += 2
            continue
        } elseif tokens[i].kind == token.T_ENCLOSE and
                (tokens[i].str == "[" or tokens[i].str == "(") {
            var bracket_pos = i
            var bracket_depth = 1
            i += 1
            while yes {
                if i > tokens_len {
                    break
                }
                if tokens[i].kind == token.T_ENCLOSE {
                    if {"(", "[", "{"}.has(tokens[i].str) {
                        bracket_depth += 1
                    } else {
                        assert({")", "]", "}"}.has(tokens[i].str))
                        bracket_depth -= 1
                        if bracket_depth <= 0 {
                            break
                        }
                    }
                }
                i += 1
            }
            i += 1
            if i > tokens_len or (
                    tokens[i].kind != token.T_DOT and
                    (tokens[i].kind != token.T_ENCLOSE or
                     not {"(", "["}.has(tokens[i].str)) and
                    tokens[i].kind != token.T_MATHASSIGN and
                    tokens[i].kind != token.T_ASSIGN) {
                if not only_assigns and can_be_call and
                        tokens[bracket_pos].str == "(" {
                    return bracket_pos
                }
                return -1
            }
            continue
        } elseif tokens[i].kind == token.T_ENCLOSE and
                tokens[i].str == "(" {
            if only_assigns {
                return -1
            }
            return i
        } elseif tokens[i].kind == token.T_MATHASSIGN or
                tokens[i].kind == token.T_ASSIGN {
            return i
        } else {
            break
        }
    }
    return -1
}

func parse(tokens, pos, msgs, project_file=none, debug=no) {
    var tokens_len = tokens.len
    var startpos = pos

    var assign_or_call_idx = check_call_or_assign_and_get_idx(
        tokens, pos, tokens_len, debug=debug
    )
    if assign_or_call_idx < 0 {
        return later none
    }
    var is_call =
        tokens[assign_or_call_idx].kind == token.T_ENCLOSE

    # Parse the call or lvalue expression:
    var v1 = expr.parse_expression(
        tokens, pos,
        msgs, max_len=(if is_call (tokens_len) else
            (assign_or_call_idx)),
        project_file=project_file, debug=debug
    ) later:
    await v1

    if is_call {
        # Check if we actually got a call expression:
        var stmt = new CallStmt()
        stmt.line = token.get_line(tokens, startpos)
        stmt.col = token.get_col(tokens, startpos)
        if v1 == none or v1.kind != ast.N_EXPR_BINOP {
            msgs.add(new msg.FileMsg(
                "Unexpected expression " +
                (if v1 != none ("of kind " +
                 ast.NodeKind.num_label(v1.kind)) else
                 ("starting with token " +
                  token.describe_token(tokens[pos]))) +
                ", expected valid call expression.",
                source_file=project_file,
                line=token.get_line(tokens, pos),
                col=token.get_col(tokens, pos),
            ))
            stmt.damaged = yes
        } elseif v1.optoken.str != "(" {
            msgs.add(new msg.FileMsg(
                "Unexpected binary operator expression " +
                token.describe_token(v1.optoken) +
                ", expected call statement with no surrounding " +
                "operator expressions, for statement starting in " +
                "line " + token.get_line(tokens, pos).as_str() +
                ", column " + token.get_col(tokens, pos).as_str() +
                ".",
                source_file=project_file,
                line=v1.optoken.line,
                col=v1.optoken.col,
            ))
            stmt.damaged = yes
        }
        if v1 == none {
            var skiplen = token.get_naive_stmt_or_expr_len(
                tokens, pos, for_expr=no
            )
            v1 = new expr.InvalidExpr(skiplen)
        }
        pos += v1.token_len
        stmt.subexprs.add(v1)

        pos = later_parse.
            parse_later_postfix(tokens, pos, stmt, msgs,
                project_file=project_file, debug=debug)
        stmt.token_len = (pos - startpos)

        return later stmt
    }
    assert(not is_call)

    var assigned_lvalue_list = [v1]
    var stmt = new AssignStmt(
        tokens[assign_or_call_idx].copy()
    )
    stmt.line = token.get_line(tokens, startpos)
    stmt.col = token.get_col(tokens, startpos)
    assert(stmt.assign_token != none)
    func check_last_assigned_expr {
        var last_assigned_expr = assigned_lvalue_list[
            assigned_lvalue_list.len]
        if last_assigned_expr == none {
            if not stmt.has_damage() {
                msgs.add(new msg.FileMsg(
                    "Unexpected token " +
                    token.describe_token_at(tokens,
                        assign_or_call_idx + 1) +
                    ", expected valid lvalue expression for " +
                    "assign operator at line " +
                    token.get_line(tokens,
                        assign_or_call_idx).as_str() +
                    ", column " +
                    token.get_col(tokens,
                        assign_or_call_idx).as_str() + ".",
                    source_file=project_file,
                    line=token.get_line(tokens, pos),
                    col=token.get_col(tokens, pos),
                ))
            }
            stmt.damaged = yes
            var skiplen = token.get_naive_stmt_or_expr_len(
                tokens, pos, for_expr=no
            )
            if skiplen < 1 {
                skiplen = 1
            }
            last_assigned_expr = new expr.InvalidExpr(skiplen)
            assigned_lvalue_list[
                assigned_lvalue_list.len] = last_assigned_expr
        }
        pos += last_assigned_expr.token_len
        if pos >= assign_or_call_idx {
            return
        }
        if pos != assign_or_call_idx {
            if pos > tokens_len or tokens[pos].kind != token.T_COMMA {
                if not stmt.has_damage() {
                    msgs.add(new msg.FileMsg(
                        "Unexpected token " +
                        token.describe_token_at(tokens,
                            pos) +
                        ", expected ',' (T_COMMA) for "
                        "next lvalue expression for " +
                        "assign operator at line " +
                        token.get_line(tokens,
                            assign_or_call_idx).as_str() +
                        ", column " +
                        token.get_col(tokens,
                            assign_or_call_idx).as_str() + ".",
                        source_file=project_file,
                        line=token.get_line(tokens, pos),
                        col=token.get_col(tokens, pos),
                    ))
                }
                stmt.damaged = yes
            } else {
                pos += 1
            }
        }
    }
    func get_next_assigned_lvalue_expr {
        assert(pos < tokens_len)
        var next_assigned = expr.parse_expression(
            tokens, pos,
            msgs, max_len=assign_or_call_idx,
            project_file=project_file, debug=debug
        ) later:
        await next_assigned

        assigned_lvalue_list.add(next_assigned)
        check_last_assigned_expr()
        return later pos < assign_or_call_idx
    }
    func get_all_assigned_lvalue_exprs {
        if pos >= assign_or_call_idx {
            return later
        }
        var do_continue = get_next_assigned_lvalue_expr() later:

        await do_continue
        if not do_continue {
            return later
        }
        do_continue = get_next_assigned_lvalue_expr() later repeat
    }

    check_last_assigned_expr()  # For the first one we already parsed.
    # Now proceed with 2th+ assigned lvalues:
    get_all_assigned_lvalue_exprs() later:

    pos = assign_or_call_idx + 1
    var assigned_rvalue_list = []
    func process_next_rvalue_expr {
        if pos > tokens_len {
            if not stmt.has_damage() {
                msgs.add(new msg.FileMsg(
                    "Unexpected end of file or stream, "
                    "expected valid assigned expression #" +
                    (assigned_rvalue_list.len + 1).as_str() + " for " +
                    "assign operator at line " +
                    token.get_line(tokens,
                        assign_or_call_idx).as_str() +
                    ", column " +
                    token.get_col(tokens,
                        assign_or_call_idx).as_str() + ".",
                    source_file=project_file,
                    line=token.get_line(tokens, assign_or_call_idx + 1),
                    col=token.get_col(tokens, assign_or_call_idx + 1),
                ))
            }
            stmt.damaged = yes
            var v2 = new expr.InvalidExpr(1)
            assigned_rvalue_list.add(v2)
            return later assigned_rvalue_list.len <
                assigned_lvalue_list.len
        }
        var v2 = expr.parse_expression(
            tokens, pos, msgs,
            project_file=project_file, debug=debug
        ) later:
        await v2

        if v2 == none {
            if not stmt.has_damage() {
                msgs.add(new msg.FileMsg(
                    "Unexpected token " +
                    token.describe_token_at(tokens,
                        assign_or_call_idx + 1) +
                    ", expected valid assigned expression #" +
                    (assigned_rvalue_list.len + 1).as_str() + " for " +
                    "assign operator at line " +
                    token.get_line(tokens,
                        assign_or_call_idx).as_str() +
                    ", column " +
                    token.get_col(tokens,
                        assign_or_call_idx).as_str() + ".",
                    source_file=project_file,
                    line=token.get_line(tokens,
                        assign_or_call_idx + 1),
                    col=token.get_col(tokens,
                        assign_or_call_idx + 1),
                ))
                stmt.damaged = yes
            }
            var skiplen = token.get_naive_stmt_or_expr_len(
                tokens, assign_or_call_idx, for_expr=no
            ) - 1
            if skiplen < 1 {
                skiplen = 1
            }
            v2 = new expr.InvalidExpr(skiplen)
        }
        assert(v2.token_len > 0)
        pos += v2.token_len
        assigned_rvalue_list.add(v2)
        if assigned_rvalue_list.len >= assigned_lvalue_list.len {
            return later no
        }
        if pos > tokens_len or tokens[pos].kind != token.T_COMMA {
            if not stmt.has_damage() {
                msgs.add(new msg.FileMsg(
                    "Unexpected token " +
                    token.describe_token_at(tokens,
                        pos) +
                    ", expected ',' (T_COMMA) for "
                    "next rvalue expression for " +
                    "assign operator at line " +
                    token.get_line(tokens,
                        assign_or_call_idx).as_str() +
                    ", column " +
                    token.get_col(tokens,
                        assign_or_call_idx).as_str() + ".",
                    source_file=project_file,
                    line=token.get_line(tokens, pos),
                    col=token.get_col(tokens, pos),
                ))
            }
            stmt.damaged = yes
        } else {
            pos += 1
        }
        return later yes
    }
    func process_all_rvalues {
        var do_continue = process_next_rvalue_expr() later:

        await do_continue
        if not do_continue {
            return later
        }
        do_continue = process_next_rvalue_expr() later repeat
    }
    process_all_rvalues() later:

    stmt.token_len = (pos - startpos)
    stmt.subexprs += assigned_lvalue_list + assigned_rvalue_list

    var newpos =
        later_parse.parse_later_postfix(tokens, pos, stmt, msgs,
            project_file=project_file, debug=debug)

    if newpos > pos and not stmt.has_damage() {
        for rvalue in assigned_rvalue_list {
            if rvalue.kind != ast.N_EXPR_BINOP or
                    rvalue.optoken.str != "(" {
                msgs.add(new msg.FileMsg(
                    "Unexpected use of 'later' for assigned "
                    "expression " +
                    ast.NodeKind.num_label(rvalue.kind) +
                    if rvalue.kind == ast.N_EXPR_BINOP
                        (" (of type '" + rvalue.optoken.str + "')")
                        else ("") + ", "
                    "can only use 'later' after call expressions.",
                    source_file=project_file,
                    line=token.get_line(tokens, pos),
                    col=token.get_col(tokens, pos),
                ))
                stmt.damaged = yes
                break
            }
        }
        for lvalue in assigned_lvalue_list {
            if lvalue.kind != ast.N_EXPR_IDREF {
                msgs.add(new msg.FileMsg(
                    "Unexpected use of 'later' for lvalue "
                    "of type " +
                    ast.NodeKind.num_label(lvalue.kind) + ", "
                    "can only use when directly assigning a "
                    "local variable name.",
                    source_file=project_file,
                    line=token.get_line(tokens, pos),
                    col=token.get_col(tokens, pos),
                ))
                stmt.damaged = yes
                break
            }
        }
    }
    pos = newpos
    stmt.token_len = (pos - startpos)
    return stmt
}

