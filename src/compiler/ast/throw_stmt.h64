## @module compiler.ast.throw_stmt
# Copyright (c) 2023-2024, ellie/@ell1e & Horse64 authors (see AUTHORS.md).
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
# 1. Redistributions of source code must retain the above copyright notice,
#    this list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above copyright notice,
#    this list of conditions and the following disclaimer in the documentation
#    and/or other materials provided with the distribution.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
# LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
# INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
# CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
# ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
# POSSIBILITY OF SUCH DAMAGE.
#
# Alternatively, at your option, this file is offered under the Apache 2
# license, see accompanied LICENSE.md.

import math from core.horse64.org

import compiler.ast as ast
import compiler.ast.expr as expr
import compiler.ast.invalid_stmt as invalid_stmt
import compiler.ast.later_parse as later_parse
import compiler.ast.block as block
import compiler.msg as msg
import compiler.token as token

type ThrowStmt base ast.StmtNode {
    var has_repeat = no

    var error_msg_failed_early_eval
    var error_msg_aot_evaluated
}

func ThrowStmt.init {
    base.init()
    self.kind = ast.N_STMT_THROW
}

func ThrowStmt.as_json_obj {
    var output = base.as_json_obj()
    if self.has_repeat {
        output["has_repeat"] = yes
    }
    return output
}

func ThrowStmt.as_code(indent=0) {
    if self.damaged {
        return " ".rep(indent) + "(damaged throw)"
    }
    var t = " ".rep(indent) + "throw"
    if self.subexprs.len > 0 {
        t += " " + self.subexprs[1].as_code(indent=indent)
    }
    return t
}

func parse(tokens, pos, msgs, project_file=none,
           is_moose64=no, debug=no) {
    var stmt = new ThrowStmt()
    var tokens_len = tokens.len
    var startpos = pos

    if pos > tokens_len or
            tokens[pos].kind != token.T_KEYWORD or
            tokens[pos].str != "throw" {
        return later none
    }

    if debug {
        print("compiler.ast.throw_stmt: parse() on " +
            tokens.sub(pos, math.min(pos + 10, tokens_len)).as_str() +
            "... with pos=" + pos.as_str() + ", "
            "tokens.len=" + tokens.len.as_str())
    }
    stmt.line = token.get_line(tokens, pos)
    stmt.col = token.get_col(tokens, pos)
    pos += 1

    if pos > tokens_len or
            not token.check_tokens_consumed_by_return(
            tokens, pos, max_len=tokens_len
            ) {
        stmt.token_len = (pos - startpos)
        return later stmt
    }

    var v = expr.parse_expression(
        tokens, pos, msgs,
        project_file=project_file, is_moose64=is_moose64
    ) later:

    await v
    if v == none {
        msgs.add(new msg.FileMsg(
            "Unexpected token " +
            token.describe_token(tokens[pos]) +
            ", expected any inline expression for throw "
            "statement starting in line " +
            token.get_line(tokens, startpos).as_str() +
            ", column " +
            token.get_col(tokens, startpos).as_str() + ".",
            kind=msg.M_ERROR, source_file=project_file,
            line=token.get_line(tokens, pos),
            col=token.get_col(tokens, pos))
        )
        stmt.damaged = yes
        var skiplen = math.max(1,
            token.get_naive_stmt_or_expr_len(
                tokens, pos, bracket_depth=0, for_expr=no,
                is_moose64=is_moose64
            ) - 1)
        var invexp = new expr.InvalidExpr(
            skiplen
        )
        pos += skiplen
        stmt.subexprs.add(invexp)
        stmt.token_len = (pos - startpos)
        return stmt
    }
    pos += v.token_len
    stmt.subexprs.add(v)
    stmt.token_len = (pos - startpos)
    return later stmt
}

