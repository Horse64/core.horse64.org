# @module compiler.ast.enum_stmt
# Copyright (c) 2023, ellie/@ell1e & Horse64 Team (see AUTHORS.md).
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
# 1. Redistributions of source code must retain the above copyright notice,
#    this list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above copyright notice,
#    this list of conditions and the following disclaimer in the documentation
#    and/or other materials provided with the distribution.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
# LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
# INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
# CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
# ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
# POSSIBILITY OF SUCH DAMAGE.
#
# Alternatively, at your option, this file is offered under the Apache 2
# license, see accompanied LICENSE.md.

import math from core.horse64.org

import compiler.ast as ast
import compiler.ast.block as block
import compiler.ast.expr as expr
import compiler.ast.invalid_stmt as invalid_stmt
import compiler.ast.type_stmt as type_stmt
import compiler.msg as msg
import compiler.token as token

type EnumStmt base ast.StmtNode {
    var label
    var enum_entry_names = []
    var enum_entry_nums = []
}

func EnumStmt.init {
    base.init()
    self.kind = ast.N_STMT_ENUM
}

func EnumStmt.as_json_obj {
    var output = base.as_json_obj()
    output["label"] = label
    return output
}

type EnumExtendStmt base ast.StmtNode {
    var enum_entry_names = []
    var enum_entry_nums = []
}

func EnumExtendStmt.init {
    base.init()
    self.kind = ast.N_STMT_ENUMEXTEND
}

func EnumExtendStmt.as_json_obj {
    var output = base.as_json_obj()
    return output
}

func parse(tokens, pos, msgs, project_file=none, debug=no) {
    var tokens_len = tokens.len
    var startpos = pos
    if pos > tokens_len or
            tokens[pos].kind != token.T_KEYWORD or
            not {"type", "enum"}.has(tokens[pos].str) {
        return later none
    }

    var stmt = if tokens[pos].str == "enum" (ast.N_STMT_ENUM)
        else (ast.N_STMT_ENUMEXTEND)
    var stmt_name = tokens[pos].str
    var headerlen = parse_extend_or_enum_or_type_header(
        stmt, tokens, tokens_len, pos, msgs,
        must_be_type=no, must_be_enum=yes,
        project_file=project_file, debug=debug
    )
    if headerlen == none {
        return later none
    }
    stmt.pos = token.get_line(tokens, startpos)
    stmt.col = token.get_col(tokens, startpos)
    pos += headerlen

    # Parse inner enum values:
    if pos > token_len or
            tokens[pos].kind != token.T_BRACKET or
            tokens[pos].str != "{" {
        pos -= 1
        var skiplen = token.get_naive_stmt_or_expr_len(
            tokens, pos, bracket_depth=0, for_expr=no
        )
        var invalid_st = new invalid_stmt.InvalidStmt(
            math.max(1, skiplen - 1)
        )
        invalid_st.line = token.get_line(tokens, startpos)
        invalid_st.col = token.get_col(tokens, startpos)
        assert(skiplen > 0)
        invalid_st.token_len = (pos - startpos) + skiplen
        return invalid_st
    }
    pos += 1  # Go past '{' bracket.

    var entry_count = 0
    while pos <= token_len and
            tokens[pos].kind == token.T_IDENT {
        stmt.enum_entry_names(tokens[pos])
        pos += 1
        entry_count += 1
        if pos + 1 < token_len and
                tokens[pos].kind == token.T_ASSIGN and
                tokens[pos + 1].kind == token.T_NUM {
            stmt.enum_entry_values(tokens[pos + 1].value)
            pos += 2
        } else {
            stmt.enum_entry_values(none)
        }
        if tokens[pos].kind == token.T_COMMA {
            pos += 1
            continue
        } elseif tokens[pos].kind == token.T_IDENT {
            if not stmt.has_damage() {
                msgs.add(new msg.FileMsg(
                    "Unexpected " + token.describe_token_at(
                        tokens, pos) +
                    ", expected ',' to resume enum entries or "
                    "'}' to end code block for " + stmt_name +
                    "statement starting in line " +
                    token.get_line(tokens, startpos).as_str() +
                    ", column " +
                    token.get_col(tokens, startpos).as_str() + ".",
                    source_file=project_file,
                    line=token.get_line(tokens, pos),
                    col=token.get_col(tokens, pos),
                ))
            }
            stmt.damaged = yes
        }
        break
    }
    if entry_count == 0 or pos > token_len or
            tokens[pos].kind != token.T_BRACKET or
            tokens[pos].str != "}" {
        if not stmt.has_damage() {
            msgs.add(new msg.FileMsg(
                "Unexpected " + token.describe_token_at(
                    tokens, pos) +
                ", expected T_IDENT for enum entry, or '}' "
                "to end code block for " + stmt_name +
                "statement starting in line " +
                token.get_line(tokens, startpos).as_str() +
                ", column " +
                token.get_col(tokens, startpos).as_str() + ".",
                source_file=project_file,
                line=token.get_line(tokens, pos),
                col=token.get_col(tokens, pos),
            )) 
        }
        stmt.damaged = yes
        while pos < token_len and (
                tokens[pos].kind != token.T_BRACKET or
                tokens[pos].str != "}") and
                not token.surely_starts_stmt_even_in_bad_code(
                tokens, pos
                ) {
            pos += 1
        }
    }
    if pos < token_len and
            tokens[pos].kind == token.T_BRACKET and
            tokens[pos].str == "}" {
        pos += 1  # Go past final close '}'.
    }
    stmt.token_len = (pos - startpos)
    return later stmt
}

