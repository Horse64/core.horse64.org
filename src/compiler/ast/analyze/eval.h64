 ## @module compiler.ast.analyze.eval
# Copyright (c) 2023-2024, ellie/@ell1e & Horse64 authors (see AUTHORS.md).
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
# 1. Redistributions of source code must retain the above copyright notice,
#    this list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above copyright notice,
#    this list of conditions and the following disclaimer in the documentation
#    and/or other materials provided with the distribution.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
# LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
# INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
# CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
# ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
# POSSIBILITY OF SUCH DAMAGE.
#
# Alternatively, at your option, this file is offered under the Apache 2
# license, see accompanied LICENSE.md.

import limit from core.horse64.org
import math from core.horse64.org

import compiler.ast as ast
import compiler.ast.expr as ast_expr

enum AnalyzeResult {
    AR_VALUE,
    AR_TYPEERROR,
    AR_UNKNOWN,
}

func try_eval_ast_node_can_this_cause_sideeffects(
        node, is_moose64=no,
        eval_by_storage_ref_callback=none) {
    if {ast.N_EXPR_LIT, ast.N_EXPR_IDREF}.has(node.kind) {
        return no
    } elseif node.kind == ast.N_EXPR_CONTAINER {
        if node.container_kind == ast_expr.C_VEC {
            return no
        }
        if {ast_expr.C_SET, ast_expr.C_LIST,
                ast_expr.C_MAP}.has(node.container_kind) {
            for v in node.subexprs {
                if try_eval_ast_node_can_this_cause_sideeffects(
                        node, is_moose64=is_moose64,
                        eval_by_storage_ref_callback=
                        eval_by_storage_ref_callback) {
                    return yes
                }
            }
            return no
        }
    } elseif node.kind == ast.N_EXPR_BINOP and
            {"+", "-", "*", "/"}.has(node.optoken.str) {
        var v1 = try_eval_ast_node_value(node.subexprs[1],
            eval_by_storage_ref_callback=eval_by_storage_ref_callback)
        var v2 = try_eval_ast_node_value(node.subexprs[2],
            eval_by_storage_ref_callback=eval_by_storage_ref_callback)
        if v1[1] != AR_VALUE or v2[1] != AR_VALUE or
                typename(v1[2]) != "num" or
                typename(v2[2]) != "num" {
            return yes
        }
        v1 = v1[2]
        v2 = v1[2]
        if v1 > limit.num_max_safe_value or
                v2 > limit.num_max_safe_value or
                v1 < -limit.num_max_safe_value or
                v2 < -limit.num_max_safe_value {
            # Outside of overflow safe range.
            return yes
        }
        return no
    } elseif node.kind == ast.N_EXPR_UNOP and
            {"-"}.has(node.optoken.str) {
        var v1 = try_eval_ast_node_value(
            node.subexprs[1], is_moose64=is_moose64,
            eval_by_storage_ref_callback=eval_by_storage_ref_callback
        )
        if v1[1] != AR_VALUE or
                typename(v1[2]) != "num" or
                v1[2] > limit.num_max_safe_value or
                v1[2] < -limit.num_max_safe_value {
            return yes
        }
        return no
    } elseif node.kind == ast.N_EXPR_BINOP and
            {"(", "."}.has(node.optoken.str) {
        if node.optoken.str == "(" and
                node.subexprs[1].kind == ast.N_EXPR_BINOP and
                node.subexprs[1].optoken.str == "." and
                node.subexprs[1].subexprs[2].kind == ast.N_EXPR_IDREF and
                node.subexprs[1].subexprs[2].label == "as_str" and
                node.subexprs[2].kind == ast.N_EXPR_CALLARGS and
                node.subexprs[2].subexprs.len == 0 {
            var v = try_eval_ast_node_can_this_cause_sideeffects(
                node, is_moose64=is_moose64,
                eval_by_storage_ref_callback=eval_by_storage_ref_callback
            )
            if v {
                return yes
            }
        }
        # The regular value evaluation might know how to handle this:
        # (since it has more special cases implemented):
        var v = try_eval_ast_node_value(
            node, is_moose64=is_moose64,
            eval_by_storage_ref_callback=eval_by_storage_ref_callback
        )
        if v[1] == AR_VALUE {
            return no
        }
        return yes
    }
    return yes
}

func try_eval_ast_node_value(
        node, is_moose64=no,
        eval_by_storage_ref_callback=none
        ) {
    if node.kind == ast.N_EXPR_LIT {
        return [AR_VALUE, node.value]
    } elseif node.kind == ast.N_EXPR_IDREF {
        if eval_by_storage_ref_callback == none or node.ref == none {
            return [AR_UNKNOWN]
        }
        var v = eval_by_storage_ref_callback(node.ref)
        if v[1] != AR_VALUE {
            return [AR_UNKNOWN]
        }
        return v
    } elseif node.kind == ast.N_EXPR_CONTAINER {
        if {ast_expr.C_SET, ast_expr.C_LIST}.has(node.container_kind) {
            var assembled_item = none
            if node.container_kind == ast_expr.C_SET {
                assembled_item = {}
            } elseif node.container_kind == ast_expr.C_LIST {
                assembled_item = []
            }
            for subnode in node.subexprs {
                if {ast.N_EXPR_IDREF, ast.N_EXPR_BINOP,
                        ast.N_EXPR_CONTAINER,
                        ast.N_EXPR_UNOP}.has(subnode.kind) {
                    var v = try_eval_ast_node_value(
                        subnode, is_moose64=is_moose64,
                        eval_by_storage_ref_callback=
                            eval_by_storage_ref_callback)
                    if v[1] == AR_TYPEERROR {
                        return [AR_TYPEERROR]
                    }
                    if v[1] == AR_UNKNOWN or not {"num", "str",
                            "bytes", "bool",
                            "none"}.has(typename(v[2])) {
                        # XXX/Note: NEVER allow sets, lists here!
                        # These must retain the reference, since
                        # we can't just insert a separate set or
                        # list copy. So we can't resolve them here.
                        return [AR_UNKNOWN]
                    }
                    assembled_item.add(v[2])
                    continue
                } elseif subnode.kind == ast.N_EXPR_LIT {
                    assembled_item.add(subnode.value)
                    continue
                }
                return [AR_UNKNOWN]
            }
            return [AR_VALUE, assembled_item]
        }
        return [AR_UNKNOWN]
    } elseif node.kind == ast.N_EXPR_BINOP {
        if node.optoken.str == "+" {
            var v1 = try_eval_ast_node_value(
                node.subexprs[1], is_moose64=is_moose64,
                eval_by_storage_ref_callback=eval_by_storage_ref_callback)
            var v2 = try_eval_ast_node_value(
                node.subexprs[2], is_moose64=is_moose64,
                eval_by_storage_ref_callback=eval_by_storage_ref_callback)
            if v1[1] == AR_TYPEERROR or v2[1] == AR_TYPEERROR {
                return [AR_TYPEERROR]
            }
            if v1[1] == AR_UNKNOWN or v2[1] == AR_UNKNOWN {
                return [AR_UNKNOWN]
            }
            v1 = v1[2]
            v2 = v2[2]
            if typename(v1) == "str" and typename(v2) == "str" {
                return [AR_VALUE, v1 + v2]
            } elseif typename(v1) == "bytes" and typename(v2) == "bytes" {
                return [AR_VALUE, v1 + v2]
            } elseif typename(v1) == "list" and typename(v2) == "list" {
                return [AR_VALUE, v1 + v2]
            } elseif typename(v1) == "set" and typename(v2) == "set" {
                return [AR_VALUE, v1 + v2]
            } elseif typename(v1) == "num" and typename(v2) == "num" {
                if v1 > limit.num_max_safe_value or
                        v2 > limit.num_max_safe_value or
                        v1 < -limit.num_max_safe_value or
                        v2 < -limit.num_max_safe_value {
                    # Outside of the range that is guaranteed not overflowing.
                    return [AR_UNKNOWN]
                }
                return [AR_VALUE, v1 + v2]
            }
            if {"num", "str", "bytes", "list", "set", "map",
                    "vec", "none"}.has(typename(v1)) and
                    typename(v1) != typename(v2) {
                return [AR_TYPEERROR]
            }
            return [AR_UNKNOWN]
        } elseif {"-", "*"}.has(node.optoken.str) {
            var v1 = try_eval_ast_node_value(
                node.subexprs[1], is_moose64=is_moose64,
                eval_by_storage_ref_callback=eval_by_storage_ref_callback)
            var v2 = try_eval_ast_node_value(
                node.subexprs[2], is_moose64=is_moose64,
                eval_by_storage_ref_callback=eval_by_storage_ref_callback)
            if v1[1] == AR_TYPEERROR or v2[1] == AR_TYPEERROR {
                return [AR_TYPEERROR]
            }
            if v1[1] != AR_VALUE or v2[1] != AR_VALUE {
                return [AR_UNKNOWN]
            }
            v1 = v1[2]
            v2 = v2[2]
            if typename(v1) != "num" or typename(v2) != "num" {
                return [AR_UNKNOWN]
            }
            if v1 > limit.num_max_safe_value or
                    v2 > limit.num_max_safe_value or
                    v1 < -limit.num_max_safe_value or
                    v2 < -limit.num_max_safe_value {
                # Outside of the range that is guaranteed not overflowing.
                return [AR_UNKNOWN]
            }
            if node.optoken.str == "-" {
                return [AR_VALUE, v1 - v2]
            }
            return [AR_VALUE, v1 * v2]
        } elseif node.optoken.str == "/" {
            var v1 = try_eval_ast_node_value(
                node.subexprs[1], is_moose64=is_moose64,
                eval_by_storage_ref_callback=eval_by_storage_ref_callback)
            var v2 = try_eval_ast_node_value(
                node.subexprs[2], is_moose64=is_moose64,
                eval_by_storage_ref_callback=eval_by_storage_ref_callback)
            if v1[1] == AR_TYPEERROR or v2[1] == AR_TYPEERROR {
                return [AR_TYPEERROR]
            }
            if v1[1] != AR_VALUE or v2[1] != AR_VALUE {
                return [AR_UNKNOWN]
            }
            v1 = v1[2]
            v2 = v2[2]
            if typename(v1) != "num" or typename(v2) != "num" {
                return [AR_UNKNOWN]
            }
            if v1 > limit.num_max_safe_value or
                    v2 > limit.num_max_safe_value or
                    v1 < -limit.num_max_safe_value or
                    v2 < -limit.num_max_safe_value or
                    math.abs(v1) < 0.00001 or
                    math.abs(v2) < 0.00001 {
                # Outside of the range that is guaranteed not overflowing.
                return [AR_UNKNOWN]
            }
            return [AR_VALUE, v1 / v2]
        } elseif node.optoken.str == "." {
            var v1 = try_eval_ast_node_value(
                node.subexprs[1], is_moose64=is_moose64,
                eval_by_storage_ref_callback=eval_by_storage_ref_callback)
            if v1[1] == AR_TYPEERROR {
                return [AR_TYPEERROR]
            }
            if v1[1] == AR_VALUE {
                v1 = v1[2]
                var v2 = node.subexprs[2]
                if v2.kind == ast.N_EXPR_IDREF and
                        v2.label == "len" and
                        {"str", "bytes", "list", "vec",
                        "map", "set"}.has(typename(v1)) {
                    return [AR_VALUE, v1.len]
                }
            }
            return [AR_UNKNOWN]
        } elseif node.kind == ast.N_EXPR_BINOP and
                node.optoken.str == "(" and
                node.subexprs[1].kind == ast.N_EXPR_BINOP and
                node.subexprs[1].optoken.str == "." and
                node.subexprs[1].subexprs[2].kind == ast.N_EXPR_IDREF and
                node.subexprs[1].subexprs[2].label == "as_str" and
                node.subexprs[2].kind == ast.N_EXPR_CALLARGS and
                node.subexprs[2].subexprs.len == 0 {
            var v = try_eval_ast_node_value(
                node.subexprs[1].subexprs[1], is_moose64=is_moose64,
                eval_by_storage_ref_callback=eval_by_storage_ref_callback)
            if v[1] == AR_TYPEERROR {
                return [AR_TYPEERROR]
            }
            if v[1] == AR_VALUE {
                if {"bytes", "str", "num", "bool", "none"}.has(
                        typename(v[2])) {
                    return [AR_VALUE, v[2].as_str()]
                }
            }
            return [AR_UNKNOWN]
        }
        return [AR_UNKNOWN]
    } elseif node.kind == ast.N_EXPR_UNOP {
        if node.optoken.str == "-" {
            var v = try_eval_ast_node_value(
                node.subexprs[1], is_moose64=is_moose64,
                eval_by_storage_ref_callback=eval_by_storage_ref_callback)
            if v[1] == AR_TYPEERROR {
                return [AR_TYPEERROR]
            }
            if v[1] == AR_VALUE {
                v = v[2]
                if typename(v) == "num" {
                    return [AR_VALUE, -v]
                }
            }
            return [AR_UNKNOWN]
        }
        return [AR_UNKNOWN]
    }
    return [AR_UNKNOWN]
}

func try_eval_ast_node_type(
        node, is_moose64=no,
        eval_by_storage_ref_callback=none
        ) {
    var v = try_eval_ast_node_value(
        node, is_moose64=is_moose64
    )
    if v[1] == AR_VALUE and
            {"num", "str", "bytes", "none"}.has(typename(v[2])) {
        return typename(v[2])
    }
    return none
}

