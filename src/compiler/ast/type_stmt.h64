## @module compiler.ast.type_stmt
# Copyright (c) 2023-2024, ellie/@ell1e & Horse64 Team (see AUTHORS.md).
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
# 1. Redistributions of source code must retain the above copyright notice,
#    this list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above copyright notice,
#    this list of conditions and the following disclaimer in the documentation
#    and/or other materials provided with the distribution.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
# LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
# INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
# CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
# ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
# POSSIBILITY OF SUCH DAMAGE.
#
# Alternatively, at your option, this file is offered under the Apache 2
# license, see accompanied LICENSE.md.

import math from core.horse64.org

import compiler.ast as ast
import compiler.ast.block as block
import compiler.ast.expr as expr
import compiler.ast.invalid_stmt as invalid_stmt
import compiler.msg as msg
import compiler.token as token

type TypeStmt base ast.StmtNode {
    var label
}

func TypeStmt.init {
    base.init()
    self.kind = ast.N_STMT_TYPE
}

func TypeStmt.as_json_obj {
    var output = base.as_json_obj()
    output["label"] = self.label
    return output
}

func TypeStmt.as_code(indent=0) {
    if self.damaged {
        return " ".rep(indent) + "(damaged type)"
    }
    var t = " ".rep(indent) + "type " + self.label
    if self.subexprs.len > 0 {
        t += " base " + self.subexprs[1].as_code(indent=indent)
    }
    t += " {\n"
    for stmt in self.subblocks[1] {
        t += stmt.as_code(indent=(indent + 4)) + "\n"
    }
    t += " ".rep(indent) + "}"
    return t
}

type TypeExtendStmt base ast.StmtNode {
}

func TypeExtendStmt.init {
    base.init()
    self.kind = ast.N_STMT_TYPEEXTEND
}

func TypeExtendStmt.as_json_obj {
    var output = base.as_json_obj()
    return output
}

func TypeExtendStmt.as_code(indent=0) {
    if self.damaged {
        return " ".rep(indent) + "(damaged type extend)"
    }
    var t = " ".rep(indent) + "extend type " +
        self.subexprs[1].as_code(indent=indent) + " {\n"
    for stmt in self.subblocks[1] {
        t += stmt.as_code(indent=(indent + 4)) + "\n"
    }
    t += " ".rep(indent) + "}"
    return t
}

enum ExtendType {
    EXTEND_NONE,
    EXTEND_TYPE,
    EXTEND_ENUM,
    EXTEND_FUNC,
    EXTEND_INVALID,
}

func get_extend_variant(
        tokens, tokens_len, pos, msgs,
        had_damage=no, project_file=none) {
    const startpos = pos
    if pos > tokens_len or
            tokens[pos].kind != token.T_KEYWORD or
            tokens[pos].str != "extend" {
        return EXTEND_NONE
    }
    pos += 1
    if pos <= tokens_len {
        if tokens[pos].str == "type" {
            return EXTEND_TYPE
        } elseif tokens[pos].str == "enum" {
            return EXTEND_ENUM
        } elseif tokens[pos].str == "func" {
            return EXTEND_FUNC
        }
    }
    if not had_damage {
        msgs.add(new msg.FileMsg(
            "Unexpected " + token.describe_token_at(
                tokens, pos) +
            ", expected either a valid custom type "
            "attribute or enum entry for 'extend' "
            "statement starting in line " +
            token.get_line(tokens, startpos).as_str() +
            ", column " +
            token.get_col(tokens, startpos).as_str() + ".",
            source_file=project_file,
            line=token.get_line(tokens, pos),
            col=token.get_col(tokens, pos),
        ))
    }
    return EXTEND_INVALID
}

func parse_extend_or_enum_or_type_header(
        stmt, tokens, tokens_len, pos, msgs,
        must_be_type=no, must_be_enum=no,
        project_file=none, debug=no
        ) {
    var clausestartpos = pos
    if pos > tokens_len or
            tokens[pos].kind != token.T_KEYWORD or
            (tokens[pos].str != "extend" and
             ((must_be_enum and tokens[pos].str != "enum") or
              (must_be_type and tokens[pos].str != "type"))) or
            not {"enum", "extend", "type"}.has(tokens[pos].str) {
        return none
    }
    var stmt_name = tokens[pos].str
    var is_type = (tokens[pos].str == "type")
    var is_enum = (tokens[pos].str == "enum")
    var is_extended = no
    if stmt_name == "extend" {
        var variant = get_extend_variant(
            tokens, tokens_len, pos, msgs, had_damage=stmt.has_damage(),
            project_file=project_file
        )
        if (must_be_type and variant != EXTEND_TYPE) or
                (must_be_enum and variant != EXTEND_ENUM) or
                variant == EXTEND_FUNC {
            return none
        } elseif variant == EXTEND_INVALID {
            stmt.damaged = yes
            # Note: error message was emitted by get_extend_variant().
            var skiplen = math.max(1,
                token.skip_in_broken_stmt_to_code_block(
                    tokens, pos, bracket_depth=0
                ))
            pos += skiplen
            return (pos - clausestartpos)
        }
        is_extended = yes
        if (variant == EXTEND_TYPE) {
            is_type = yes
        } else {
            assert(variant == EXTEND_ENUM)
            is_enum = yes
        }
        pos += 2  # Go past "extend enum"/"extend func".
    } else {
        pos += 1  # Go past "enum"/"type".
    }

    if pos > tokens_len or
            tokens[pos].kind != token.T_IDENT {
        if not stmt.has_damage() {
            msgs.add(new msg.FileMsg(
                "Unexpected " + token.describe_token_at(tokens,
                    pos) +
                ", expected valid expression for " + stmt_name + " "
                "statement starting in line " +
                token.get_line(tokens, clausestartpos).as_str() +
                ", column " +
                token.get_col(tokens, clausestartpos).as_str() + ".",
                source_file=project_file,
                line=token.get_line(tokens, pos),
                col=token.get_col(tokens, pos),
            ))
        }
        stmt.damaged = yes
        var skiplen = math.max(1,
            token.skip_in_broken_stmt_to_code_block(
                tokens, pos, bracket_depth=0
            ))
        pos += skiplen
        return (pos - clausestartpos)    
    }
    var extend_type_name = tokens[pos].str
    var extend_type_path = none
    pos += 1

    if is_extended {
        # Extract components of type prefix, if any:
        var extend_type_path = none
        while pos + 1 <= tokens_len and
                tokens[pos].kind == token.T_DOT and
                tokens[pos + 1].kind == token.T_IDENT {
            if extend_type_path == none {
                extend_type_path = []
            }
            extend_type_path.add(extend_type_name)
            extend_type_name = tokens[pos + 1].str
            pos += 2
        }
        var iexp = new expr.IdRefExpr()
        iexp.label = extend_type_name
        iexp.type_path = extend_type_path
        stmt.subexprs.add(iexp)
    } else {
        stmt.label = extend_type_name
    }

    if is_type and pos <= tokens_len and
            tokens[pos].kind == token.T_KEYWORD and
            tokens[pos].str == "base" {
        pos += 1
        var base_line, base_col
        if pos <= tokens_len {
            base_line = tokens[pos].line
            base_col = tokens[pos].col
        } else {
            base_line = tokens[pos - 1].line
            base_col = tokens[pos - 1].col
        }
        var basecount = 0
        while yes {
            if (pos > tokens_len or
                    tokens[pos].kind != token.T_IDENT) and
                    basecount <= 0 {
                if not stmt.has_damage() {
                    msgs.add(new msg.FileMsg(
                        "Unexpected " + token.describe_token_at(
                            tokens,  pos) +
                        ", expected T_IDENT for base class for " +
                        stmt_name + " "
                        "statement starting in line " +
                        token.get_line(tokens,
                            clausestartpos).as_str() +
                        ", column " +
                        token.get_col(tokens,
                            clausestartpos).as_str() + ".",
                        source_file=project_file,
                        line=token.get_line(tokens, pos),
                        col=token.get_col(tokens, pos),
                    ))
                }
                stmt.damaged = yes
                break
            }
            var base_type_name = tokens[pos].str
            var base_type_path = none
            basecount += 1
            pos += 1

            while pos + 1 <= tokens_len and
                    tokens[pos].kind == token.T_DOT and
                    tokens[pos + 1].kind == token.T_IDENT {
                if base_type_path == none {
                    base_type_path = []
                }
                base_type_path.add(base_type_name)
                base_type_name = tokens[pos + 1].str
                pos += 2
            }
            var iexp = new expr.IdRefExpr()
            iexp.line = base_line
            iexp.col = base_col
            iexp.label = base_type_name
            iexp.type_path = base_type_path
            stmt.subexprs.add(iexp)

            # XXX: If we ever wanted to allow multiple base types
            # again, here would be the place to check for
            # token.T_COMMA and resume.
            break
        }
    }
    if pos <= tokens_len and (
            tokens[pos].kind != token.T_ENCLOSE or
            tokens[pos].str != "{") {
        if not stmt.has_damage() {
            msgs.add(new msg.FileMsg(
                "Unexpected " + token.describe_token_at(tokens,
                    pos) +
                ", expected '{' for code block "
                "for " + stmt_name + " statement starting in line " +
                token.get_line(tokens,
                    clausestartpos).as_str() +
                ", column " +
                token.get_col(tokens,
                    clausestartpos).as_str() + ".",
                source_file=project_file,
                line=token.get_line(tokens, pos),
                col=token.get_col(tokens, pos),
            ))
        }
        stmt.damaged = yes
        var skiplen = math.max(1,
            token.skip_in_broken_stmt_to_code_block(
                tokens, pos, bracket_depth=0
            ))
        pos += skiplen
    }
    return (pos - clausestartpos)
}

func check_type_block_contents(type_obj) {
    var stmt_name = "type"
    if type_obj.kind == ast.N_STMT_TYPEEXTEND {
        stmt_name = "extend type"
    } elseif type_obj.kind == ast.N_STMT_STRUCT {
        stmt_name = "struct"
    }
    for stmt in type_obj.subblocks[1] {
        if stmt.kind != ast.N_STMT_VAR {
            msgs.add(new msg.FileMsg(
                "Unexpected statement of kind " +
                    ast.NodeKind.num_label(stmt.kind) +
                ", expected var statement inside "
                "code block "
                "for " + stmt_name + " statement starting in line " +
                type_obj.line.as_str() +
                ", column " +
                type_obj.col.as_str() + ".",
                source_file=project_file,
                line=stmt.line,
                col=stmt.col,
            ))
            stmt.damaged = yes
        }
    }
}

func parse(tokens, pos, msgs, project_file=none,
           is_morse64=no, debug=no) {
    var tokens_len = tokens.len
    var startpos = pos
    if pos > tokens_len or
            tokens[pos].kind != token.T_KEYWORD or
            not {"extend", "type"}.has(tokens[pos].str) or
            (tokens[pos].str == "extend" and
             (pos + 1 > tokens_len or
              tokens[pos + 1].str != "type")) {
        return later none
    }

    var stmt = if tokens[pos].str == "type" (new TypeStmt())
        else (new TypeExtendStmt())
    var stmt_name = tokens[pos].str
    var headerlen = parse_extend_or_enum_or_type_header(
        stmt, tokens, tokens_len, pos, msgs,
        must_be_type=yes, must_be_enum=no,
        project_file=project_file, debug=debug
    )
    if headerlen == none {
        return later none
    }
    stmt.line = token.get_line(tokens, startpos)
    stmt.col = token.get_col(tokens, startpos)
    pos += headerlen

    # Parse inner code block:
    var block_result = block.parse(
        tokens, pos, msgs, project_file=project_file,
        debug=debug, had_damage=stmt.has_damage(),
        block_purpose_name="for the " + stmt_name + " "
            "statement starting in "
            "line " + token.get_line(tokens, startpos).as_str() +
            ", column " + token.get_col(tokens, startpos).as_str()
    ) later:

    await block_result
    if block_result.has_damage() {
        stmt.damaged = yes
    } elseif not stmt.damaged {
        check_type_block_contents(stmt)
    }
    pos += block_result.token_len
    stmt.subblocks.add(block_result.stmts)
    stmt.token_len = (pos - startpos)
    return later stmt
}

