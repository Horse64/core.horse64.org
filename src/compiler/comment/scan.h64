# @module compiler.comment.scan
# Copyright (c) 2023, ellie/@ell1e & Horse64 Team (see AUTHORS.md).
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
# 1. Redistributions of source code must retain the above copyright notice,
#    this list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above copyright notice,
#    this list of conditions and the following disclaimer in the documentation
#    and/or other materials provided with the distribution.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
# LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
# INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
# CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
# ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
# POSSIBILITY OF SUCH DAMAGE.
#
# Alternatively, at your option, this file is offered under the Apache 2
# license, see accompanied LICENSE.md.

import io from core.horse64.org
import net.fetch from core.horse64.org
import uri from core.horse64.org

import compiler.comment.command as command_comment

func scan_uri_for_comments(file_uri, allow_vfs=no, allow_disk=yes,
        strip_noncommands=no, c_style_comments=no,
        only_doc_comments=yes) {
    var contents = net.fetch.get_str(
        file_uri, allow_disk=allow_disk, allow_vfs=allow_vfs
    ) later:

    await contents
    var comments = command_comment.get_comments_from_str(
        contents, stop_at_noncomment=no,
        only_doc_comments=only_doc_comments,
        strip_noncommands=strip_noncommands,
        c_style_comments=c_style_comments
    )
    return comments
}

type TreeCommentScanner {
    var allow_vfs protect = no
    var allow_disk protect = yes
    var _walker
    var _outstanding_files = []
    var strip_noncommands
    var dir_path
    var dir_is_vfs = no
}

func TreeCommentScanner.init(dir_uri, strip_noncommands=no,
        exclude_dir_names=[], exclude_dot_names=yes) {
    if not {"file", "vfs"}.has(uri.get_protocol(dir_uri)) {
        throw new NotImplementedError("Protocol not "
            "implemented for scanning.")
    }
    self.dir_path = uri.to_file_or_vfs_path(dir_uri)
    self.dir_is_vfs = (uri.get_protocol(dir_uri) == "vfs")
    self._walker = io.tree_list_walker(
        self.dir_path, relative=no,
        return_dirs=no,
        exclude_dot_names=exclude_dot_names,
        exclude_dir_names=exclude_dir_names,
        allow_vfs=self.dir_is_vfs,
        allow_disk=not self.dir_is_vfs)
    self.strip_noncommands = strip_noncommands
}

func TreeCommentScanner.produce_next_file_path {
    if self._outstanding_files.len > 0 {
        return self._outstanding_files.pop()
    }
    var entries = self._walker.walk() later:

    await entries
    if entries == none {
        return none
    }
    var endings = [".h64", ".c", ".h"]
    for entry in entries {
        var ending_found = no
        for ending in endings {
            if entry.lower().ends(ending) {
                ending_found = yes
                break
            }
        }
        if ending_found {
            self._outstanding_files.add(entry)
        }
    }

    if self._outstanding_files.len > 0 {
        return self._outstanding_files.pop()
    }
    entries = self._walker.walk() later repeat
}

func TreeCommentScanner.produce_next_comments {
    var next_file_path = self.produce_next_file_path() later:

    await next_file_path
    if next_file_path == none {
        return none
    }
    var file_uri = if self.dir_is_vfs
        (uri.from_vfs_path(next_file_path)) else
        (uri.from_disk_path(next_file_path))
    var comments = scan_uri_for_comments(
        file_uri,
        only_doc_comments=yes,
        strip_noncommands=self.strip_noncommands,
        c_style_comments=no,
        allow_vfs=self.dir_is_vfs,
        allow_disk=not self.dir_is_vfs) later:

    await comments
    return comments
}

