# Copyright (c) 2020-2023, ellie/@ell1e & Horse64 Team (see AUTHORS.md).
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
# 1. Redistributions of source code must retain the above copyright notice,
#    this list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above copyright notice,
#    this list of conditions and the following disclaimer in the documentation
#    and/or other materials provided with the distribution.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
# LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
# INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
# CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
# ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
# POSSIBILITY OF SUCH DAMAGE.
#
# Alternatively, at your option, this file is offered under the Apache 2
# license, see accompanied LICENSE.md.

import text from core.horse64.org

import compiler.token as token

func verify_tokens_with_list(tokens, str_list, code_str=none) {
    i = 1
    while i <= tokens.len {
        if i > str_list.len {
            throw new ValueError(
                "Given tokens have excess entries, "
                "first unexpected is entry #" + i.as_str() +
                " -> " + tokens[i].as_str())
        }
        if tokens[i].str != str_list[i] {
            entry = [tokens[i].str, str_list[i]]
            throw new ValueError(
                "Given tokens have wrong entry, expected " +
                token.as_escaped_code_string(
                str_list[i]) + " but got entry #" +
                i.as_str() + " -> " +
                tokens[i].as_str() + "\nEntire list is: " +
                tokens.as_str())
        }
        i += 1
    }
    if i < str_list.len {
        throw new ValueError(
            "Given tokens have too few entries, only #" +
            tokens.len.as_str() + " while expected " +
            "were #" + str_list.len)
    }
}

func helper_for_tokenizer_test_and_verify(
        inputcode, expected_strs, with_whitespace=no
        ) {
    # Call tokenizer:
    var result = token.tokenize_str(
        inputcode, keep_whitespace=with_whitespace,
    )

    # Verify token text contents and no error:
    verify_tokens_with_list(
        result.tokens, expected_strs, code_str=inputcode
    )
    assert(result.msgs.len == 0)

    # Verify token line/col positioning:
    var n = 0
    for tok in result.tokens {
        n += 1
        var pos = text.pos_from_line_col(
            inputcode, tok.line, tok.col
        )
        if pos == none or inputcode[pos] != tok.str[1] {
            throw new ValueError("token #" + n.as_str() +
                " has line,col " + tok.line.as_str() +
                "," + tok.col.as_str() + " but the original "
                "code string, that maps to " + if pos != None
                ("the differing character " +
                token.describe_char_at(inputcode, pos)) else
                ("nowhere") + " and not to this -> " + tok.as_str())
        }
    }
}

func test_get_str_widechar_escaping_len {
    assert(token.get_str_widechar_escaping_len(
        "x\\u0001f3c6", 2
    ) == 10)
}

func test_parse_token_value {
    var t
    t = token.Token("5", token.T_NUM, none, none)
    assert(token.parse_token_value(t) == 5)
    t = token.Token("0xfe", token.T_NUM, none, none)
    assert(token.parse_token_value(t) == 254)
    t = token.Token("'\\x20\\x21'", token.T_STR, none, none)
    assert(token.parse_token_value(t) == " !")
    t = token.Token('b"\\x20\\x21\0"', token.T_BYTES, none, none)
    assert(token.parse_token_value(t) == b" !\0")
    t = token.Token('b"\\u0001f3c6"', token.T_BYTES, none, none)
    assert(token.parse_token_value(t) == "🏆".as_bytes())
}

func test_token_list_from_str_list {
    var result
    result = token.token_list_from_str_list([
        "func", "bla", ".", "main", "{", "}"
    ])
    assert(result.len == 6)
    assert(result[1].kind == token.T_KEYWORD)
    assert(result[2].kind == token.T_IDENT)
    assert(result[2].str == "bla")

    result = token.token_list_from_str_list([
        "func", "bla", ".", "main", "{",
        "var", "x", "=", "-", "y", "}"
    ])
    assert(result.len == 11)
    assert(result[9].str == "-")
    assert(result[9].kind == token.T_UNARYMATH)

    result = token.token_list_from_str_list([
        "func", "bla", ".", "main", "{",
        "var", "x", "=", "u", "-", "y", "}"
    ])
    assert(result.len == 12)
    assert(result[9].kind == token.T_IDENT)
    assert(result[10].str == "-")
    assert(result[10].kind == token.T_MATH)
}

func test_unary_op {
    var result = token.tokenize_str(
        'v = (\n # Hello\n-(5)\n}',
        keep_comments=yes)
    assert(result.tokens.len > 5)
    assert(result.tokens[5].kind == token.T_UNARYMATH)
    assert(result.tokens[5].str == "-")
    var result = token.tokenize_str(
        'v = (1\n # Hello\n-(5)\n}',
        keep_comments=yes)
    assert(result.tokens.len > 6)
    assert(result.tokens[6].kind == token.T_MATH)
    assert(result.tokens[6].str == "-")
}

func test_surely_starts_stmt_even_in_bad_code {
    var result, tokens

    tokens = token.token_list_from_str_list([
        "(", "var", "x"])
    result = token.surely_starts_stmt_even_in_bad_code(
        tokens, 1, break_out_of_code_blocks=yes
    )
    assert(result == no)
    tokens = token.token_list_from_str_list([
        "(", "var", "x"])
    result = token.surely_starts_stmt_even_in_bad_code(
        tokens, 2, break_out_of_code_blocks=yes
    )
    assert(result == yes)
    tokens = token.token_list_from_str_list([
        "(", "var", "x"])
    result = token.surely_starts_stmt_even_in_bad_code(
        tokens, 2, break_out_of_code_blocks=no
    )
    assert(result == no)
}

func test_get_naive_stmt_or_expr_len {
    var result, tokens

    tokens = token.token_list_from_str_list([
        "func", "bla", ".", "main", "{", "}", "+", "a", "b"])
    result = token.get_naive_stmt_or_expr_len(
        tokens, 1, for_expr=no)
    assert(result == 4)
    tokens = token.token_list_from_str_list([
        "func", "bla", ".", "main", "+", "{", "}", "+", "a", "b"])
    result = token.get_naive_stmt_or_expr_len(
        tokens, 1, for_expr=no)
    assert(result == 9)
    tokens = token.token_list_from_str_list([
        "import", "compiler", ".", "msg", "as", "msg",
        "import", "compiler", ".", "token", "as", "token"])
    result = token.get_naive_stmt_or_expr_len(
        tokens, 1, for_expr=no)
    assert(result == 6)
    tokens = token.token_list_from_str_list([
        "{", "var", "x", "=", "5", "print", "(", "none", ")", "}",
        "print", "(", "y", ")"])
    result = token.get_naive_stmt_or_expr_len(
        tokens, 1, for_expr=no)
    assert(result == 10)
    tokens = token.token_list_from_str_list([
        "var", "x", "+", "}"])
    result = token.get_naive_stmt_or_expr_len(
        tokens, 1, for_expr=no)
    assert(result == 3)
    tokens = token.token_list_from_str_list([
        "(", "var", "x", "+", "}"])
    result = token.get_naive_stmt_or_expr_len(
        tokens, 1, for_expr=no)
    assert(result == 1)

    # Special case that is different for expressions:
    tokens = token.token_list_from_str_list([
        "2", "+", "3",
        "new", "z", "(", ")", "x"])
    var expr_len = token.get_naive_stmt_or_expr_len(
        tokens, 1, for_expr=yes)
    assert(expr_len == 3)
}

func test_tokenize {
    # Basic test example:
    helper_for_tokenizer_test_and_verify(
        'func main {print("Hello! \\nTest")}',
        ["func", "main", "{", "print", "(",
        '"Hello! \\nTest"', ")", "}"])

    # Test some numbers thing:
    helper_for_tokenizer_test_and_verify(
        'var x = ((5 + 3 ** 4.3) >= 2)',
        ["var", "x", "=", "(", "(",
        "5", "+", "3", "**", "4.3", ")", ">=", "2",
        ")"])

    # Test escapes in a string literal:
    helper_for_tokenizer_test_and_verify(
        'func main {print(b"Hello! \\" \\nTest")}',
        ["func", "main", "{", "print", "(",
        'b"Hello! \\" \\nTest"', ")", "}"])

    # Test whitespace:
    helper_for_tokenizer_test_and_verify(
        'func main {\nprint(b"Hello! \\" \\nTest")}',
        ["func", " ", "main", " ", "{", "\n", "print", "(",
        'b"Hello! \\" \\nTest"', ")", "}"],
        with_whitespace=yes)

    # Test wrong brackets:
    var result = token.tokenize_str(
        'func main {(\nprint(b"Hello! \\" \\nTest")}'
        )
    assert(result.msgs.len > 0)
}

