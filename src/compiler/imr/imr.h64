## @module compiler.imr
# Copyright (c) 2025, ellie/@ell1e & Horse64 authors (see AUTHORS.md).
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
# 1. Redistributions of source code must retain the above copyright notice,
#    this list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above copyright notice,
#    this list of conditions and the following disclaimer in the documentation
#    and/or other materials provided with the distribution.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
# LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
# INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
# CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
# ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
# POSSIBILITY OF SUCH DAMAGE.
#
# Alternatively, at your option, this file is offered under the Apache 2
# license, see accompanied LICENSE.md.

import textfmt from core.horse64.org
import time from core.horse64.org

import compiler.ast as ast
import compiler.compiler_log as clog
import compiler.storage.ref as st_ref
import compiler.msg as msg

type IMRTranslationResult {
    var project_file_to_imr_map = {->}

    var msgs = []
}

type IMRNode {
    var kind
    var line
    var col
}

func IMRNode.init(kind) {
    self.kind = kind
}

type IMRInstruction base IMRNode {
    var is_block = no
    var blocks = []
}

type IMRBlock base IMRNode {
    var is_block = yes
    var instructions = []
}

func IMRBlock.as_str {
    var t = "IMRBlock{" +
        "kind=" + IMRNodeKind.num_label(
            self.kind) +
        ",instructions=" +
        self.instructions.as_str()
    return t + "}"
}

type IMRFuncBlock base IMRBlock {
    var is_func_attr = no

    var global_func_id = none
    var global_type_id = none
    var type_attr_id = none
    var func_scope = none
    var label = none
    var storage_ref = none
}

func IMRFuncBlock.init(
        func_scope, label,
        storage_ref,
        ) {
    base.init(IMR_BLOCK_FUNC)
    self.func_scope = func_scope
    self.label = label + ""
    if storage_ref == none or
            not has_attr(storage_ref, "kind") or
            (storage_ref.kind != st_ref.ST_GLOBAL and
             storage_ref.kind != st_ref.ST_GLOBALATTR) {
        throw new ValueError(
            "No storage_ref was specified, or "
            "it's not of kind ST_GLOBAL or "
            "ST_GLOBALATTR."
        )
    }
    self.is_func_attr =
        storage_ref.kind == st_ref.ST_GLOBALATTR
}

func IMRFuncBlock.as_str {
    var t = "IMRFuncBlock{" +
        "kind=" + IMRNodeKind.num_label(
            self.kind) +
        ",label=" + textfmt.as_escaped_code_string(
            self.label) + 
        ",is_func_attr=" + self.is_func_attr.as_str()
    t += ",storage_ref=" +
        self.storage_ref.as_str()
    t += ",instructions=" +
        self.instructions.as_str()
    return t + "}"
}

enum IMRNodeKind {
    IMR_BLOCK_FUNC,
    IMR_BLOCK_COND,
    IMR_BLOCK_CONDLOOP,
    IMR_BLOCK_ITERLOOP,
    IMR_BLOCK_STRUCT,
    IMR_BLOCK_TYPE,
    IMR_BLOCK_DO,
    IMR_BLOCK_DO_C,
    IMR_BLOCK_RESCUE,
    IMR_BLOCK_FINALLY,
    IMR_BLOCK_WITH,
    IMR_INST_MOVE,
    IMR_INST_MOVE_CAST,
    IMR_INST_BINOP,
    IMR_INST_CALL,
    IMR_INST_BREAK,
    IMR_INST_CONTINUE,
    IMR_INST_RETURN,
    IMR_INST_AWAIT,
}

type IMRForFile {
    var project_file

    var blocks = []
}

func generate_imr_for_expr(node) {
    
}

func generate_imr_for_stmt(node) {
    
}

func generate_imr_for_file(pfile, is_moose64=no) {
    const program_name =
        if is_moose64 ("moosec") else ("horsec")
    var result = new IMRForFile()

    pfile.ensure_ast() later:

    for stmt in pfile.ast.stmts {
        if stmt.kind == ast.N_STMT_FUNC {
            var func_scope = stmt.func_scope
            assert(func_scope != none)
            var block = none
            if stmt.type_path != none and
                    stmt.type_path.len > 0 {
                assert(func_scope.is_type_attr)
                var type_id =
                    func_scope.owning_type_storage_id
                var attr_id =
                    func_scope.owning_type_scope.
                        name_to_symbol_map[
                            stmt.label
                        ].storage_id
                if type_id == none or attr_id == none {
                    var stmt_str = stmt.as_str()
                    print(program_name + ": error: "
                        "Found invalid func stmt with "
                        "invalid storage. pfile=" +
                        pfile.as_str() + " "
                        "label=" + textfmt.as_escaped_code_string(
                            stmt.label) + " "
                        "stmt=" +
                        stmt_str.sub(1, 100) +
                        "... "
                        "type_id=" + type_id.as_str() + " "
                        "attr_id=" + attr_id.as_str())
                    throw new RuntimeError(
                        "Internal error, encountered func "
                        "stmt with broken storage."
                    )
                }
                block = new IMRFuncBlock(
                    func_scope, stmt.label,
                    new st_ref.StorageRef(
                        st_ref.ST_GLOBALATTR,
                        [type_id, attr_id])
                )
            } else {
                block = new IMRFuncBlock(
                    func_scope, stmt.label,
                    new st_ref.StorageRef(
                        st_ref.ST_GLOBAL,
                        func_scope.storage_id
                    ),
                )
            }
            result.blocks.add(block)
        }
    }
    return later result
}

func generate_imr_for_project(
        project,
        build_options=none,
        msgs=none, is_moose64=no, debug=no,
        ) {
    const program_name =
        if is_moose64 ("moosec") else ("horsec")
    var boptions = build_options
    var imrresult = new IMRTranslationResult()

    if boptions.print_stages {
        clog.log_or_print(
            new msg.FileMsg(
                "stage imr: " +
                "Launched on project=" +
                project.as_str() + " "
                "[t=" + time.ticks().as_str() + "]",
                kind=msg.M_INFO,
            ),
            msgs=imrresult.msgs, program_name=program_name,
            force_print=not boptions.format_output_for_scripts,
        )
    }
    if debug {
        print(program_name + ": debug: "
            "generate_imr_for_project(): "
            "Starting per file loop for "
            "project=" + project.as_str() + ", "
            "is_moose64=" +
                is_moose64.as_str())
    }

    func process_file(pfile) {
        if boptions.print_stages {
            clog.log_or_print(
                new msg.FileMsg(
                    "stage imr: " +
                    "Processing file pfile=" +
                    pfile.as_str() + " "
                    "[t=" + time.ticks().as_str() + "]",
                    kind=msg.M_INFO,
                ),
                msgs=imrresult.msgs,
                program_name=program_name,
                force_print=not boptions.format_output_for_scripts,
            )
        }
        var result = generate_imr_for_file(
            pfile, is_moose64=is_moose64)
        later:

        await result
        return later yes
    }
    var result = project.do_for_all_files(
        process_file
    ) later:

    await result
    assert(result == yes or result == no)

    return later no
}

