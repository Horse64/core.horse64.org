## @module compiler.moose64.builtins
# Copyright (c) 2024, ellie/@ell1e & Horse64 authors (see AUTHORS.md).
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
# 1. Redistributions of source code must retain the above copyright notice,
#    this list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above copyright notice,
#    this list of conditions and the following disclaimer in the documentation
#    and/or other materials provided with the distribution.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
# LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
# INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
# CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
# ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
# POSSIBILITY OF SUCH DAMAGE.
#
# Alternatively, at your option, this file is offered under the Apache 2
# license, see accompanied LICENSE.md.

import compiler.ast as ast
import compiler.builtin_syms as builtin_syms
import compiler.storage.ref as st_ref
import compiler.moose64.c_importer as m64_c_importer

func get_m64_c_type_builtin_by_id(project, storage_id) {
    assert(typename(storage_id) == "num")
    var st = project.builtin_syms_storage

    if not st.storage_id_to_item_map.has(storage_id) {
        return none
    }
    var st_item = st.storage_id_to_item_map[storage_id]
    if not st_item.is_c_builtin_type {
        return none
    }
    return st_item
}

func get_m64_c_type_builtin_ref_by_name(
        project, name, create_if_missing=no,
        create_type=none
        ) {
    if create_if_missing and create_type == none {
        throw new ValueError("For create_if_missing=yes, "
            "the create_type option must be specified as well.")
    }
    var item = builtin_syms.get_item_by_name(
        project, name, create_if_missing=create_if_missing,
        create_type=create_type,
        must_be_c_type=yes,
        in_module_name=none, in_package_name=none
    )
    if item != none {
        return item.symbol_storage_ref.copy()
    }
    return none
}

func is_ast_node_referring_to_m64_builtin_assert(
        project_file, node, is_moose64=no) {
    return is_ast_node_referring_to_m64_builtin_by_name(
        project_file, node,
        "assert", in_module_name=none,
        in_package_name=none,
        is_moose64=is_moose64)
}

func is_ast_node_referring_to_m64_memory_size_of(
        project_file, node, is_moose64=no) {
    return is_ast_node_referring_to_m64_builtin_by_name(
        project_file, node,
        "size_of", in_module_name="memory",
        in_package_name="m64.horse64.org",
        is_moose64=is_moose64)
}

func is_ast_node_referring_to_m64_memory_as_ref(
        project_file, node, is_moose64=no) {
    return is_ast_node_referring_to_m64_builtin_by_name(
        project_file, node,
        "as_ref", in_module_name=none,
        in_package_name=none,
        is_moose64=is_moose64)
}

func get_m64_debug_assert_func_storage_ref(
        func_name, project=none, is_moose64=no) {
    if project == none or not is_moose64 {
        return none
    }
    for pfile_uri in project.module_uri_to_file_map {
        var pfile = project.module_uri_to_file_map[
            pfile_uri
        ]
        if pfile.modinfo == none or
                pfile.modinfo.module_name != "debug" or
                pfile.modinfo.package_name != "m64.horse64.org" {
            continue
        }
        for st_id in pfile.global_scope.storage_id_to_entry {
            var entry =
                pfile.global_scope.storage_id_to_entry[st_id]
            if entry.kind != ast.N_STMT_FUNC or
                    entry.label != func_name {
                continue
            }
            return new st_ref.StorageRef(
                st_ref.ST_GLOBAL, st_id
            )
        }
    }
    return none
}

func get_m64_debug_assert_func_name_of_ast_node(
        project_file, node, is_moose64=no) {
    if not is_moose64 {
        return none
    }
    if (node.kind != ast.N_EXPR_IDREF or
            not node.label.starts("assert")) and
            (node.kind != ast.N_EXPR_BINOP or
            node.optoken.str != "." or
            node.subexprs.len < 2 or
            node.subexprs[2].kind != ast.N_EXPR_IDREF or
            not node.subexprs[2].label.starts("assert")) {
        return none
    }
    if node == ast.N_EXPR_BINOP {
        node = node.subexprs[2]
    }
    if node.ref == none or
            node.ref.kind != st_ref.ST_GLOBAL {
        return none
    }
    
    for pfile_uri in project_file.project.module_uri_to_file_map {
        var pfile = project_file.project.module_uri_to_file_map[
            pfile_uri
        ]
        if pfile.modinfo == none or
                pfile.modinfo.module_name != "debug" or
                pfile.modinfo.package_name != "m64.horse64.org" {
            continue
        }
        for st_id in pfile.global_scope.storage_id_to_entry {
            var entry =
                pfile.global_scope.storage_id_to_entry[st_id]
            if entry.kind != ast.N_STMT_FUNC or
                    (entry.label != "assert" and
                     not entry.label.startswith("assert_")) {
                continue
            }
            if node.ref.id == st_id {
                return entry.label
            }
        }
    }
    return none
}

func is_ast_node_referring_to_m64_memory_deref(
        project_file, node, is_moose64=no) {
    return is_ast_node_referring_to_m64_builtin_by_name(
        project_file, node,
        "deref", in_module_name=none,
        in_package_name=none,
        is_moose64=is_moose64)
}

func is_ast_node_referring_to_m64_builtin_by_name(
        project_file, node,
        builtin_name, in_module_name=none,
        in_package_name=none,
        is_moose64=no) {
    if not is_moose64 {
        return no
    }
    var builtin_info =
        builtin_syms.get_item_by_name(
            project_file.project, builtin_name,
            in_module_name=in_module_name,
            in_package_name=in_package_name)
    var as_ref_storage_ref =
        builtin_info.symbol_storage_ref
    if node.kind == ast.N_EXPR_IDREF and
            node.label == builtin_name and
            node.ref != none and
            node.ref.kind == st_ref.ST_MCREF and
            node.ref.alike(
                as_ref_storage_ref
            ) {
        return yes
    } elseif in_module_name != none and
            node.kind == ast.N_EXPR_BINOP and
            node.optoken.str == "." and
            node.subexprs.len >= 2 and
            node.subexprs[2].kind == ast.N_EXPR_IDREF and
            node.subexprs[2].label == builtin_name and
            node.subexprs[2].ref != none and
            node.subexprs[2].ref.kind == st_ref.ST_MCREF and
            node.subexprs[2].ref.alike(
                as_ref_storage_ref
            ) {
        return yes
    }
    return no
}

func register_builtins_with_project(project) {
    var id

    id = m64_c_importer.get_mcref_id(project)
    builtin_syms.register_symbol(project, "size_of",
        new st_ref.StorageRef(st_ref.ST_MCREF, id),
        builtin_syms.BI_FUNC, ["memory"],
        func_signature="(<-any)->size",
        package_name="m64.horse64.org",
        is_moose64=yes) later:

    id = m64_c_importer.get_mcref_id(project)
    builtin_syms.register_symbol(project, "as_ref",
        new st_ref.StorageRef(st_ref.ST_MCREF, id),
        builtin_syms.BI_FUNC, "$any",
        func_signature="(<-any)->any ref",
        is_moose64=yes) later:

    id = m64_c_importer.get_mcref_id(project)
    builtin_syms.register_symbol(project, "deref",
        new st_ref.StorageRef(st_ref.ST_MCREF, id),
        builtin_syms.BI_FUNC, "$any",
        func_signature="(<-any ref)->any",
        is_moose64=yes) later:

    id = m64_c_importer.get_mcref_id(project)
    builtin_syms.register_symbol(project, "assert",
        new st_ref.StorageRef(st_ref.ST_MCREF, id),
        builtin_syms.BI_FUNC, "$any",
        func_signature="(<-any ref)->any",
        is_moose64=yes) later:

    return later
}

